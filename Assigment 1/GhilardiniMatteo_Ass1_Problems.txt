Author: Matteo Ghilardini

Problem 1: starts at line 8
Problem 2: starts at line 41
Problem 3: starts at line 188
Problem 4: starts at line 340

------------------------ PROBLEM 1 ------------------------
# Alphabeth {0,1}

# Accepted Inputs: 0011 10101010 00111001
# Rejected Inputs: 1 11100 0101010

input: '001110' 
blank: ' '
start state: start
table:
  start:
    0: {write: 'x', R: have0}
    1: {write: 'y', R: have1}
    # Last check:
    [x,y] : R
    ' ' : {L: accept}
  have0:
    [0,x,y] : R
    1: {write: 'y', L: back}
    ' ' : {R: reject}
  have1:
    [1,x,y] : R
    0: {write: 'x', L: back}
    ' ' : {R: reject}
  back:
    [0,1,x,y] : L
    ' ': {R: start}
  accept:
  
  reject:



------------------------ PROBLEM 2 ------------------------
# Alphabeth {a, ..., f, #, |}

# Accepted Inputs: 
#   acc|ac#ffac#acc#cac#aacc
#   abcdef|abcdef#aabcdef#def
#   ab|ab

# Rejected Inputs:
#   c|ac#bc#cc#dc
#   cba|a#cba#
#   fea|efa#afe#aef#cea

input: 'abc|abc#' 
blank: ' '
start state: start
table:
  start:
    ['|', '#', ' ']: {R: reject}
    [a,b,c,d,e,f] : {R: checkFormat}
    
  checkFormat:
    [a,b,c,d,e,f]: R
    ['|']: {R: arrayFormat}
    ['#']: {R: reject}
    
  arrayFormat:
    [a,b,c,d,e,f]: R
    ['|']: {R: reject}
    ['#']: {R: haveHash}
    [' ']: {L: checkLetter}
    
  haveHash:
    [a,b,c,d,e,f]: {R: arrayFormat}
    ['|', '#', ' ']: {R: reject}
    
  checkLetter:
    [' ','|']: {R: reject}
    [a]: {write: x, L: haveA}
    [b]: {write: x, L: haveB}
    [c]: {write: x, L: haveC}
    [d]: {write: x, L: haveD}
    [e]: {write: x, L: haveE}
    [f]: {write: x, L: haveF}
    ['#']: {L: reset}
    
  reset:
    [a,b,c,d,e,f,'#','|']: L
    [A]: {write: a, L}
    [B]: {write: b, L}
    [C]: {write: c, L}
    [D]: {write: d, L}
    [E]: {write: e, L}
    [F]: {write: f, L}
    ' ': {R: nextWord}
    
  
  haveA:
    [a,b,c,d,e,f, '#']: L
    '|': {L: searchA}
  haveB:
    [a,b,c,d,e,f, '#']: L
    '|': {L: searchB}
  haveC:
    [a,b,c,d,e,f, '#']: L
    '|': {L: searchC}
  haveD:
    [a,b,c,d,e,f, '#']: L
    '|': {L: searchD}
  haveE:
    [a,b,c,d,e,f, '#']: L
    '|': {L: searchE}
  haveF:
    [a,b,c,d,e,f, '#']: L
    '|': {L: searchF}
    
  letterFound:
    ' ': {R: checkFullMatch}
    [a,b,c,d,e,f,A,B,C,D,E,F,'|']: {R: continue}
  
  checkFullMatch:
    [a,b,c,d,e,f,'#','|']: R
    [A]: {write: a, R}
    [B]: {write: b, R}
    [C]: {write: c, R}
    [D]: {write: d, R}
    [E]: {write: e, R}
    [F]: {write: f, R}
    [x]: {L: isCompleteMatch}
    
  isCompleteMatch:
    ['#', '|']: {R: accept}
    [a,b,c,d,e,f]: {R: nextWord}
    
  continue:
    [a,b,c,d,e,f,A,B,C,D,E,F,'#','|']: R
    x: {L: checkLetter}
    
  
  searchA:
    a: {write: A, L: letterFound}
    [A,B,C,D,E,F]: L
    [b,c,d,e,f]: {R: noMatch}
  searchB:
    b: {write: B, L: letterFound}
    [A,B,C,D,E,F]: L
    [a,c,d,e,f]: {R: noMatch}
  searchC:
    c: {write: C, L: letterFound}
    [A,B,C,D,E,F]: L
    [a,b,d,e,f]: {R: noMatch}
  searchD:
    d: {write: D, L: letterFound}
    [A,B,C,D,E,F]: L
    [a,b,c,e,f]: {R: noMatch}
  searchE:
    e: {write: E, L: letterFound}
    [A,B,C,D,E,F]: L
    [a,b,c,d,f]: {R: noMatch}
  searchF:
    f: {write: F, L: letterFound}
    [A,B,C,D,E,F]: L
    [a,b,c,d,e]: {R: noMatch}
    
  noMatch:
    [A]: {write: a, R}
    [B]: {write: b, R}
    [C]: {write: c, R}
    [D]: {write: d, R}
    [E]: {write: e, R}
    [F]: {write: f, R}
    '|': {R: nextWord}
    
  nextWord:
    [a,b,c,d,e,f,'#',x, '|']: R
    ' ': {L: clearWord}
  clearWord:
    [a,b,c,d,e,f,x]: {write: ' ', L}
    '#': {write: ' ', L: checkLetter}
    '|': {R: reject}
  
  accept:
    
  reject:
    
    
    
------------------------ PROBLEM 3 ------------------------
// Accepted Inputs:
//		00000010-1=000001
//		01010-001010=0000000
//		1100-10=1010

// Rejected Inputs:
//		00000010-1=010001
//		01010-001011=0000000
//		11000-=11000

//-------CONFIGURATION
name: problem3
init: start
accept: accept

// STARTING POINT, CHECK BASE FORMAT
start,0,_,_
	copy,0,_,_,-,-,-
start,1,_,_
	copy,1,_,_,-,-,-
start,-,_,_
	reject,-,_,_,-,-,-

// COPY THE FIRST TERM
copy,0,_,_
	copy,_,0,_,>,>,-
copy,0,0,_
	copy,_,0,_,>,>,-
copy,0,1,_
	copy,_,0,_,>,>,-
copy,1,_,_
	copy,_,1,_,>,>,-
copy,1,0,_
	copy,_,1,_,>,>,-
copy,1,1,_
	copy,_,1,_,>,>,-
copy,-,_,_
	hasMinus,_,_,_,>,-,-
    
hasMinus,-,_,_
	reject,_,_,_,-,-,-
hasMinus,=,_,_
	reject,_,_,_,-,-,-
hasMinus,0,_,_
	skipNumber,0,_,_,>,-,-
hasMinus,1,_,_
	skipNumber,1,_,_,>,-,-

// SKIP THE SECOND TERM
skipNumber,1,_,_
	skipNumber,1,_,_,>,-,-
skipNumber,0,_,_
	skipNumber,0,_,_,>,-,-
skipNumber,=,_,_
	subtract,=,_,_,<,<,-

// SUBTRACTION
subtract,0,0,_ // 0-0 = 0
    subtract,0,0,0,<,<,<
subtract,0,1,_ // 1-0 = 1
    subtract,0,1,1,<,<,<
subtract,_,1,_ // 1-null = 1
    subtract,_,_,1,-,<,<
subtract,_,0,_ // 0-null = 0
    subtract,_,_,0,-,<,<
subtract,0,_,_ // null-0 = 0
    subtract,0,_,0,<,-,<
subtract,1,1,_ // 1-1 = 0
	subtract,1,_,0,<,<,<
subtract,1,0,_ // 0-1 = 1, borrow 1
    borrow,1,0,1,<,<,<
subtract,1,_,_ // null-1 = 1, borrow 1
    borrow,1,_,1,-,-,<
subtract,_,_,_
	check,_,_,_,>,-,>
    
borrow,_,0,_
	borrow,_,1,1,-,<,<
borrow,_,1,_
	subtract,_,0,0,-,<,<
borrow,0,0,_
	borrow,0,1,1,-,<,<
borrow,0,1,_
	subtract,0,0,0,-,<,<
borrow,1,0,_
	subtract,1,0,0,-,<,<
borrow,1,_,_
	subtract,1,_,0,<,<,<
borrow,1,1,_
	borrow,1,1,1,-,<,<

// DONE WITH SUBTRACTION, SO CHECK RESULT
check,0,_,_
	check,_,_,_,>,-,-
check,1,_,_
	check,_,_,_,>,-,-
check,1,_,0
	check,_,_,0,>,-,-
check,0,_,1
	check,_,_,1,>,-,-
check,0,_,0
	check,_,_,0,>,-,-
check,1,_,1
	check,_,_,1,>,-,-
check,=,_,0
	gotoEnd,_,_,0,>,-,-
check,=,_,1
	gotoEnd,_,_,1,>,-,-
    
// GOTO END
gotoEnd,0,_,0
	gotoEnd,0,_,0,>,-,>
gotoEnd,0,_,1
	gotoEnd,0,_,1,>,-,>
gotoEnd,1,_,0
	gotoEnd,1,_,0,>,-,>
gotoEnd,1,_,1
	gotoEnd,1,_,1,>,-,>
gotoEnd,_,_,0
	gotoEnd,_,_,0,-,-,>
gotoEnd,0,_,_
	gotoEnd,0,_,_,>,-,-
gotoEnd,_,_,1
	gotoEnd,_,_,1,-,-,>
gotoEnd,1,_,_
	gotoEnd,1,_,_,>,-,-
gotoEnd,_,_,_
	compare,_,_,_,<,-,<
    
// COMPARE THE SUBTRACTION AND THE RESULT
compare, 1,_,1
	compare,1,_,1,<,-,<
compare, 0,_,0
	compare,0,_,0,<,-,<
compare, _,_,_
	accept,_,_,_,-,-,-
compare, 1,_,0
	reject,_,_,_,-,-,-
compare, 0,_,1
	reject,_,_,_,-,-,-
compare,_,_,0
	compare,_,_,0,-,-,<
compare, _,_,1
	reject,_,_,1,-,-,-
compare, 1,_,_
	reject,1,_,_,-,-,-
compare, 0,_,_
	compare,0,_,_,<,-,-



------------------------ PROBLEM 4 ------------------------
// Accepted Inputs:
//		1010/10=101
//		1010/11=11
//		1000010/1010=110

// Rejected Inputs:
//		1010/10=100
//		10100/11=11
//		1000011/1010=111

//-------CONFIGURATION
name: problem4
init: start
accept: accept

// STARTING POINT, CHECK BASE FORMAT
start,0,_,_
	removeZeros,_,_,_,>,-,-
start,1,_,_
	skip,1,_,_,-,-,-
start,/,_,_
	reject,-,_,_,-,-,-

removeZeros,0,_,_
	removeZeros,_,_,_,>,-,-
removeZeros,1,_,_
	skip,1,_,_,>,-,-

skip,0,_,_
	skip,0,_,_,>,-,-
skip,1,_,_
	skip,1,_,_,>,-,-
skip,/,_,_
	copy,/,_,_,>,-,-
    
copy,0,_,_
	copy,_,0,_,>,>,-
copy,1,_,_
	copy,_,1,_,>,>,-
copy,=,_,_
	prepareCompute,=,_,_,<,-,-
copy,/,_,_
	reject,/,_,_,-,-,- // incorrect format
    
prepareCompute,_,_,_
	prepareCompute,_,_,_,<,-,-
prepareCompute,/,_,_
	subtract,/,_,_,<,<,-
    
subtract,0,0,_ // 0-0 = 0
    subtract,0,0,_,<,<,-
subtract,1,0,_ // 1-0 = 1
    subtract,1,0,_,<,<,-
subtract,1,_,_ // 1-null = 1
    subtract,1,_,_,<,<,-
subtract,0,_,_ // 0-null = 0
    subtract,0,_,_,<,<,-
subtract,_,0,_ // null-0 = 0
    subtract,0,0,_,<,-,-
subtract,1,1,_ // 1-1 = 0
	subtract,0,1,_,<,<,-
subtract,0,1,_ // 0-1 = 1, borrow 1
    borrow,1,1,_,<,<,-
subtract,_,_,_
	checkMSB,_,_,_,>,>,-
subtract,_,1,_ // null-1 => not valid
    checkResult,_,1,_,>,<,-

checkMSB,1,_,_
	subtractAgain,1,_,_,>,>,-
checkMSB,1,0,_
	subtractAgain,1,_,_,>,>,-
checkMSB,1,1,_
	subtractAgain,1,1,_,>,>,-
checkMSB,0,_,_
	subtractAgain,_,_,_,>,>,-
checkMSB,_,0,_
	subtractAgain,_,_,_,>,>,-
checkMSB,0,1,_
	subtractAgain,_,1,_,>,>,-
checkMSB,0,0,_
	checkResult,_,_,_,>,<,-
checkMSB,_,1,_
	checkResult,_,_,_,>,<,-
    
borrow,0,_,_
	borrow,1,_,_,<,<,-
borrow,1,_,_
	subtract,0,_,_,<,<,-
borrow,0,0,_
	borrow,1,0,_,<,<,-
borrow,1,0,_
	subtract,0,0,_,<,<,-
borrow,0,1,_
	take,0,1,_,<,<,-
borrow,1,1,_
	take,1,1,_,<,<,-
borrow,_,_,_
	checkResult,_,_,_,>,-,-

take,1,0,_
	subtract,0,0,_,<,<,-
take,1,_,_
	subtract,0,_,_,<,<,-
take,0,_,_
	take,1,_,_,<,<,-
take,_,_,_
	checkResult,_,_,_,>,<,-

subtractAgain,0,_,_
	subtractAgain,0,_,_,>,>,-
subtractAgain,0,0,_
	subtractAgain,0,0,_,>,>,-
subtractAgain,1,_,_
	subtractAgain,1,_,_,>,>,-
subtractAgain,1,1,_
	subtractAgain,1,1,_,>,>,-
subtractAgain,0,1,_
	subtractAgain,0,1,_,>,>,-
subtractAgain,1,0,_
	subtractAgain,1,0,_,>,>,-
subtractAgain,/,_,_
	increaseCounter,/,_,_,-,-,<
  
increaseCounter,/,_,_
	subtract,/,_,1,<,<,>
increaseCounter,/,_,0
	subtract,/,_,1,<,<,>
increaseCounter,/,_,1
	increaseMSB,/,_,0,-,-,<

increaseMSB,/,_,_
	toggleOnesUntilLSB,/,_,1,-,-,>
increaseMSB,/,_,0
	toggleOnesUntilLSB,/,_,1,-,-,>
increaseMSB,/,_,1
	increaseMSB,/,_,1,-,-,<
    
toggleOnesUntilLSB,/,_,_
	subtract,/,_,_,<,<,-
toggleOnesUntilLSB,/,_,0
	toggleOnesUntilLSB,/,_,0,-,-,>
toggleOnesUntilLSB,/,_,1
	toggleOnesUntilLSB,/,_,0,-,-,>
    
checkResult,0,_,_
	checkResult,0,_,_,>,-,-
checkResult,1,_,_
	checkResult,1,_,_,>,-,-
checkResult,/,_,_
	checkResult,/,_,_,>,-,-
checkResult,_,_,_
	checkResult,_,_,_,>,-,-
checkResult,=,_,_
	goLSB,=,_,_,>,-,-

goLSB,0,_,_
	goLSB,0,_,_,>,-,-
goLSB,1,_,_
	goLSB,1,_,_,>,-,-
goLSB,_,_,_
	compareResults,_,_,_,<,-,<

compareResults,0,_,0
	compareResults,0,_,0,<,-,<
compareResults,0,_,_
	compareResults,0,_,_,<,-,<
compareResults,1,_,1
	compareResults,1,_,1,<,-,<
compareResults,=,_,_
	accept,_,_,_,-,-,-